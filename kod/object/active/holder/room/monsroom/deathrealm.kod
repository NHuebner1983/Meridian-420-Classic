// Meridian 59, Copyright 1994-2012 Andrew Kirmse and Chris Kirmse.
// All rights reserved.
//
// This software is distributed under a license that is described in
// the LICENSE file that accompanies it.
//
// Meridian is a registered trademark.


////////////////////////////////////////////////////////////////////////////////
DeathRealm is MonsterRoom

constants:

   include blakston.khd

resources:

   room_name_DeathRealm ="Death Realm"
   death_realm_no_cast_rsc = "The forces of Qor prevent you from using that magic here. "

   // Lava Damage
   Lava_damage_no_hurt = "You walk through the lava as if you were a god."
   Lava_damage_hurt = "As you wade through the lava it burns you violently."      
   Lava_scratch_snd = frying.ogg

   // Heat Damage
   Heat_damage_hurt = "You feel your skin burn by the overwhelming heat."

classvars:

   vrName = room_name_DeathRealm
   viWeatherZone = WEATHER_ZONE_RAZA
   viWeatherMask = WEATHER_MASK_DEFAULT_NS

properties:

   prBackground = background_chaos_night
   prMapChanges = TRUE
   prMonsters = TRUE
   plGenerators = $
   piMonster_count_max = 40
   piInit_count_min = 5
   piInit_count_max = 10
   
   // Lava damage
   piLava_damage = 5
   
   // Heat damage
   piHeat_damage_min = 1
   piHeat_damage_max = 4
   piHeat_notice = 30000
   
messages:

   Constructed()
   {
      if prMapChanges = TRUE
      {
         Send(self,@ChangeTexture,#id=10,#new_texture=03203,#flags=CTF_FLOOR);
         Send(self,@ChangeTexture,#id=0,#new_texture=09738,#flags=CTF_FLOOR);
         Send(self,@ChangeTexture,#id=5,#new_texture=09738,#flags=CTF_FLOOR);
         Send(self,@ChangeTexture,#id=15,#new_texture=09738,#flags=CTF_BELOWWALL);
         Send(self,@ChangeTexture,#id=11,#new_texture=03203,#flags=CTF_BELOWWALL);
      }

      if prMonsters = TRUE
      {
         plMonsters = [ [&Skeleton, 8], 
                        [&BatteredSkeleton, 8],
                        [&TuskedSkeleton, 8],
                        [&HeadlessSkeleton, 6],
                        [&DuskSkeleton, 6],
                        [&GhostSkeleton, 5],
                        [&Mummy, 7], 
                        [&BlackMummy, 6],
                        [&SpectralMummy, 6],
                        [&Zombie, 5], 
                        [&Spirit, 5],
                        [&HalfKnight, 5],
                        [&DaemonSkeleton, 5],
                        [&NecromancerTroop, 5],
                        [&CaptainKnight, 5], 
                        [&Thrasher, 4],
                        [&GiantDaemonSkeleton, 2],
                        [&Ghost, 2], 
                        [&DarkAngel, 1],
                        [&BlackDragon, 1]
                         ];
      }

      propagate;
   }
   
   RecalcBackgroundSkyGraphic(iSkyBox=0)
   {
      return;
   }

   GetRoomLight()
   {
      local iLight;

      return iLight=0;
   }

   ReqSpellCast(who = $, oSpell = $, lItems = $)
   {
      if IsClass(oSpell,&Elusion)
         OR IsClass(oSpell,&Rescue)
         OR IsClass(oSpell,&DeathRift)
         OR IsClass(oSpell,&Truce)
         OR IsClass(oSpell,&Jig)
         OR IsClass(oSpell,&NightVision)
         OR IsClass(oSpell,&MartyrsBattleground)
         OR IsClass(oSpell,&Anonymity)
         OR IsClass(oSpell,&ForcesOfLight)
         OR IsClass(oSpell,&QorBane)
         OR IsClass(oSpell,&PortalofLife)
         OR IsClass(oSpell,&FinalRites)
         OR IsClass(oSpell,&BrambleWall)
      {
         // We have to provide the fail message here.
         Send(who,@MsgSendUser,#message_rsc=death_realm_no_cast_rsc,
               #parm1=Send(oSpell,@GetName));

         return FALSE;
      }

      propagate;
   }

   SomethingMoved(what = $, new_row = $, new_col = $, fine_row = FINENESS/2,
                  fine_col = FINENESS/2, cause = CAUSE_UNKNOWN, speed = 0,
                  non_monsters_only = FALSE)
   {
      local i, temp, each_obj, packet_built, lNode, iQflags, 
            iRflags, iHeightF, iHeightFWD, iHeightC, iServerID, session;

      if (IsClass(what, &User)) {
	     session = Send(what,@GetSession);
         lUser_pos = [Send(self,@GetTeleportAngle),new_row,new_col,fine_row,fine_col,session];
	  }

      propagate;
   }

   NewHold(what = $, new_angle = ANGLE_EAST, new_row = 1, new_col = 1,
           fine_row = FINENESS/2, fine_col = FINENESS/2, session = $,
           merge = TRUE)
   {
      local bUser;

      if what = $
         OR what = self
      {
         return;
      }

      bUser = IsClass(what,&User);
	  
	  if (bUser) {
         pbUser = what;
	  }

      if bUser
      {
		 if (bUser AND ptLava_damage = $) {
		    ptLava_damage = CreateTimer(self, @CheckForLavaDamage, 1000);
		 }
		 if (bUser AND ptHeat_damage = $) {
		    ptHeat_damage = CreateTimer(self, @HeatDamageTimer, 3000);
		 }
      }

      propagate;
   }

   LastUserLeft()
   {
      if (ptLava_damage <> $)
      {
		 DeleteTimer(ptLava_damage);
         ptLava_damage = $;
      }

      if (ptHeat_damage <> $)
      {
		 DeleteTimer(ptHeat_damage);
         ptHeat_damage = $;
      }

      propagate;
   }

   Delete()
   {
      if (ptLava_damage <> $)
      {
		 DeleteTimer(ptLava_damage);
         ptLava_damage = $;
      }

      if (ptHeat_damage <> $)
      {
		 DeleteTimer(ptHeat_damage);
         ptHeat_damage = $;
      }

      propagate;
   }
   
   CheckForLavaDamage()
   {
      local iSectorID;
      
	  ptLava_damage = $;
      ptLava_damage = CreateTimer(self, @CheckForLavaDamage, 1000);
	  
      if (pbUser = $ OR lUser_pos = $) {
	     return;
	  }

	  iSectorID = Send(self,@GetSectorIDAtLocation,#row=Nth(lUser_pos,2),
		  		  #col=Nth(lUser_pos,3),#fine_row=Nth(lUser_pos,4),
				  #fine_col=Nth(lUser_pos,5));
				  
      if (iSectorID = 10)
	  {
	     // Burn the player with lava...
		 Send(self, @BurnPerson, #what=pbUser);
	  }
	  
	  return;
   }
   
   BurnPerson(what = $)
   {
      if (what <> $ AND IsClass(what, &Player) AND NOT IsClass(what, &DM))
      {
		 Send(what,@LoseHealth,#amount=piLava_damage,#precision=FALSE);
         Send(what,@WaveSendUser,#wave_rsc=Lava_scratch_snd);
		 Send(what,@MsgSendUser,#message_rsc=Lava_damage_hurt);
      }

      if (what <> $ AND IsClass(what, &DM))
      {
         Send(what,@WaveSendUser,#wave_rsc=Lava_scratch_snd);
		 Send(what,@MsgSendUser,#message_rsc=Lava_damage_no_hurt);
      }
	  
      return;
   }
   
   HeatDamageTimer()
   {
	  ptHeat_damage = $;
      ptHeat_damage = CreateTimer(self, @HeatDamageTimer, 3000);
   	  
      if (pbUser = $) {
	     return;
	  }
	  
	  // Do heat damage on player...
      Send(self, @DoHeatDamage, #what=pbUser);
	  
	  return;
   }
   
   DoHeatDamage(what = $)
   {
      if (what <> $ AND IsClass(what, &Player) AND NOT IsClass(what, &DM))
      {
		 Send(what,@LoseHealth,#amount=Random(piHeat_damage_min, piHeat_damage_max),#precision=FALSE);
         
		 piHeat_notice += 1000;
		 
		 if (piHeat_notice > 30000)
		 {
		    Send(what,@MsgSendUser,#message_rsc=Heat_damage_hurt);
			piHeat_notice = 0;
		 }
      }

      return;
   }
   
end
////////////////////////////////////////////////////////////////////////////////
