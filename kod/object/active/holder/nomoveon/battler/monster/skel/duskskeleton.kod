// Meridian 59, Copyright 1994-2012 Andrew Kirmse and Chris Kirmse.
// All rights reserved.
//
// This software is distributed under a license that is described in
// the LICENSE file that accompanies it.
//
// Meridian is a registered trademark.


////////////////////////////////////////////////////////////////////////////////
DuskSkeleton is Skeleton

constants:

   include blakston.khd

   POISON_CHANCE = 6
   POISON_DURATION = 30000   //// in milliseconds
   POISON_LOSSRATE = 10000    //// in health points * 10^-4 / second

resources:

   duskskeleton_koc_name_rsc = "teotnapyicoa"
   duskskeleton_name_rsc = "dusk skeleton"
   duskskeleton_icon_rsc = skelbody.bgf

   duskskeleton_desc_rsc = \
      "The air around the skeleton is dark and thick with evil.  The smell of "
      "death and disease linger on its fetid breath as it darts towards "
      "its target.  The origins of this abomination are unknown.  The only "
      "definite knowledge known is of the deadly poison it carries "
      "in its bite."

   duskskeleton_poisoned_add_rsc = \
      "As the poison travels to your head, you begin "
      "to feel a little light-headed.  The room spins around you."
   duskskeleton_illusion_poisoned = \
      "%s%s reels as poison from your attack courses through %s veins."

   duskskeleton_dead_icon_rsc = skelX.bgf
   duskskeleton_dead_name_rsc = "dead dusk skeleton"

   duskskeleton_sound_hit = skl_atkm.ogg
   duskskeleton_sound_miss = skl_atkm.ogg
   duskskeleton_sound_death = skl_dth.ogg
   duskskeleton_sound_aware = skl_awr.ogg

classvars:

   vrKocName = duskskeleton_koc_name_rsc
   vrName = duskskeleton_name_rsc
   vrIcon = duskskeleton_icon_rsc
   vrDesc = duskskeleton_desc_rsc
   vrDead_icon = duskskeleton_dead_icon_rsc
   vrDead_name = duskskeleton_dead_name_rsc
   viDead_drawfx = DRAWFX_BLACK

   viTreasure_type = TID_TOUGH

   viSpeed = SPEED_VERY_FAST
   viAttack_type = ATCK_WEAP_CLAW
   viAttributes = 0
   viLevel = 110
   viDifficulty = 6
   // Attack range of 192, or 3 row/col.
   viAttackRange = 192
   viVisionDistance = 11
   viKarma = -100
   viDefault_behavior = \
      AI_FIGHT_AGGRESSIVE | AI_FIGHT_SWITCHALOT | AI_MOVE_REGROUP
   viLight_Penalty = 3
   viCashmin = 250
   viCashmax = 750

   vrSound_hit = duskskeleton_sound_hit
   vrSound_miss = duskskeleton_sound_miss
   vrSound_aware = duskskeleton_sound_aware
   vrSound_death = duskskeleton_sound_death

properties:

   piAnimation = ANIM_NONE
   piDrawfx = DRAWFX_BLACK

messages:

   Constructed()
   {
      plResistances = [ [ATCK_WEAP_PIERCE, 70 ],
                        [ATCK_WEAP_THRUST, 20 ],
                        [-ATCK_SPELL_UNHOLY, 70 ],
                        [-ATCK_SPELL_SHOCK, 70 ],
                        [-ATCK_SPELL_COLD, 70 ],
                        [-ATCK_SPELL_FIRE, -10 ],
                        [-ATCK_SPELL_HOLY, -20 ],
                        [ATCK_WEAP_BLUDGEON, -20 ]
                      ];

      propagate;
   }

   MonsterAttack(what=$)
   {
      Send(self,@DoSlash);

      return;
   }

   DoSlash()
   {
      piAnimation = ANIM_ATTACK;
      Send(poOwner,@SomethingChanged,#what=self);
      piAnimation = ANIM_NONE;

      return;
   }

   NewOwner(what=$)
   "Make room darker when present, about 2X darker than a ShadowBeast"
   {
      //New room
      if what <> $
      {
         Send(what,@AddBaseLight,#amount=-viLight_Penalty);
      }

      //Old room
      if poOwner <> $
      {
         Send(poOwner,@AddBaseLight,#amount=viLight_Penalty);
      }

      propagate;
   }

   HitSideEffect(what=$,who=$)
   {
      local oSpell;

      if Random(1,POISON_CHANCE) = 1
      {
         oSpell = Send(SYS,@FindSpellByNum,#num=SID_POISON);
         Send(oSpell,@MakePoisoned,#who=what,
               #lossrate=POISON_LOSSRATE,#duration=POISON_DURATION);

         if IsClass(what,&Player)
         {
            Send(what,@EffectSendUserDuration,#effect=EFFECT_WAVER,
                  #duration=10000);
            Send(what,@MsgSendUser,#message_rsc=duskskeleton_poisoned_add_rsc);
         }
         
         if who <> $
         {
            Send(who,@MsgSendUser,#message_rsc=duskskeleton_illusion_poisoned,
                  #parm1=Send(what,@GetDef),#parm2=Send(what,@GetName),
                  #parm3=Send(what,@GetHisHer));
         }
      }

      return;
   }

   IsAlly(target=$)
   {
      // Qor Characters?
      return FALSE;
   }

   Killed()
   {
      // old room
      if poOwner <> $
      {
         Send(poOwner,@AddBaseLight,#amount=viLight_Penalty);
      }

      propagate;
   }

   Delete()
   {
      if poOwner <> $
      {
         Send(poOwner,@AddBaseLight,#amount=viLight_Penalty);
      }

      propagate;
   }

end
////////////////////////////////////////////////////////////////////////////////
